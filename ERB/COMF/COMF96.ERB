@COM96
;애널봉사
CALL PRINT_TRAIN_NAME(SELECTCOM)

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 96

;実行できない
SIF TCVAR:28 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = %STR:2%

;-------------------------------------------------
;사정체크
;-------------------------------------------------
IF CHECK_COM("Ａ자극손훑기")
	CALL SAMEN_CHECK, 70
	SELECTCOM = PREVCOM
	CALL SAMEN_CHECK, 50
	SELECTCOM = 96
ELSE
	CALL SAMEN_CHECK
ENDIF

RETURN 1

@FLAG_COM96, ARG
SIF TEQUIP:넘어뜨리기 == 0
	CALL SETFLAG, "Ａ봉사플래그", TARGET
SIF CHECK_COM("Ａ자극손훑기") == 96
	CALL FLAG_COM90, 50
;대상の입⇔조교자のAの汚れが移動
CALL EQ_STAIN, TARGET, PLAYER, 0, 4

@SOURCE_COM96, ARG
SIF CHECK_COM("Ａ자극손훑기") == 96
	CALL SOURCE_COM90, 50

VARSET LOCAL

LOCAL:20 += 10
LOCAL:21 += 100

LOCAL:8 = COM_ORDER_STAIN("Ａ", "Ｍ")*100 + 100
LOCAL:11 = CALCF("실행치による욕정추가")
LOCAL:14 = 3000

;汚れデータを元に, LOCAL:8は上で決定

;ABL:봉사정신をみる
SELECTCASE ABL:봉사정신
CASE 0
	LOCAL:4 = 250
	LOCAL:5 = 50
CASE 1
	LOCAL:4 = 300
	LOCAL:5 = 100
CASE 2
	LOCAL:4 = 350
	LOCAL:5 = 200
CASE 3
	LOCAL:4 = 400
	LOCAL:5 = 300
CASE 4
	LOCAL:4 = 450
	LOCAL:5 = 500
CASEELSE
	LOCAL:4 = 500
	LOCAL:5 = 750
ENDSELECT

IF TEQUIP:넘어뜨리기
	LOCAL:4 += 1000
ELSE
	LOCAL:13 = 5000
ENDIF

;ABL:기교をみる
SELECTCASE ABL:기교
CASE 0
	TIMES LOCAL:4 , 0.50
	TIMES LOCAL:5 , 0.50
CASE 1
	TIMES LOCAL:4 , 0.80
	TIMES LOCAL:5 , 0.80
CASE 2
CASE 3
	TIMES LOCAL:4 , 1.20
	TIMES LOCAL:5 , 1.20
CASE 4
	TIMES LOCAL:4 , 1.50
	TIMES LOCAL:5 , 1.50
CASEELSE
	TIMES LOCAL:4 , 2.00
	TIMES LOCAL:5 , 2.00
ENDSELECT


CALL CALC, "조교소스", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@TRAIN_MESSAGE_COM96
;同時넘어뜨리기
IF TEQUIP:Ｗ넘어뜨리기
	PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 
	;조수が前回と同じ行動(違う行動の時は, ここでなく, EQUIP_COM32で記述)
	SIF SELECTCOM:1 == PREVCOM:1 && SELECTCOM:1 >= 0
		PRINTFORM %TEXTS("Ｗ넘어뜨리기 중")%
	IF SELECTCOM == PREVCOM
		PRINTFORMW %CALLNAME:MASTER%의 애널에, 혀를 기게해 계속 핥아 돌리고 있다…
	ELSE
		IF ABL:봉사정신 >= 3 && ABL:새드끼 >= 3 && RAND:2 == 0
			PRINTFORMW %CALLNAME:MASTER%의 엉덩이 구멍을, 뾰족하게 세운 혀로 안까지 핥았다…
		ELSEIF ABL:봉사정신 >= 3
			PRINTFORMW %CALLNAME:MASTER%의 애널을, 철저하게 혀로 핥아서 꺠끗하게 했다…
			STAIN:MASTER:Ａ = SET_STAIN(0, "Ａ")
		ELSE
			PRINTFORMW %CALLNAME:MASTER%의 엉덩이 구멍을, 할짝할짝 혀로 깨끗하게 했다…
		ENDIF
	ENDIF
ELSEIF CHECK_COM("３Ｐ봉사")
	CALL TRAIN_MESSAGE_3P
ELSE
	PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 
	;前回역애널
	IF PREVPLAY("Ａ인서트", PLAYER)
		PRINTFORM , 물고 있던 것이 없어져도 입을 여전히 슬쩍 연 상태인 %CALLNAME:PLAYER%의 애널
		IF ABL:봉사정신 >= 3 && RAND:2 == 0
			PRINTW 을 혀로 봉사했다…
		ELSE
			PRINTW 에 혀를 기게했다…
		ENDIF
	ELSE
		IF CHECK_COM("Ａ자극손훑기")
			IF PREVCOM == 97
				LOCALS = %CLO("팬티훑기")%
				LOCALS:1 = %LOCALS%
			ELSE
				LOCALS = 손
				LOCALS:1 = 손바닥
			ENDIF
			IF TALENT:연모 && RAND:4 == 0
				PRINTFORM 페니스를 사랑스러운 듯이 %조사처리(LOCALS,"로")% 훑으면서, 
			ELSEIF ABL:기교 >= 3 && ABL:봉사정신 >= 3 && RAND:3 == 0
				PRINTFORM 귀두를 %조사처리(LOCALS:1,"로")% 주물러 돌리면서, 
			ELSEIF ABL:기교 >= 3 && RAND:2 == 0
				PRINTFORM 페니스를 절묘한 힘조절로 자극하면서, 
			ELSEIF RAND:2 == 0
				PRINTFORM 페니스를 천천히 %조사처리(LOCALS,"로")% 훑으면서, 
			ELSE
				PRINTFORM 페니스를 %조사처리(LOCALS,"로")% 훑으면서, 
			ENDIF
		ENDIF
		IF ABL:봉사정신 >= 3 && ABL:새드끼 >= 3 && RAND:2 == 0
			PRINTFORMW %CALLNAME:PLAYER%의 엉덩이 구멍을 뾰족하게 세운 혀로 안까지 핥았다…
		ELSEIF ABL:봉사정신 >= 3
			PRINTFORMW 정성스럽게 애널을 혀로 핥아 깨끗하게 하기 시작했다…
			STAIN:PLAYER:Ａ = SET_STAIN(0, "Ａ")
		ELSEIF ABL:새드끼 >= 3 || TEQUIP:넘어뜨리기
			PRINTFORMW %CALLNAME:PLAYER%의 엉덩이 구멍을 혀로 깨끗하게 했다…
		ELSE
			PRINTFORMW %CALLNAME:PLAYER%의 애널을 조심조심 혀로 핥았다…
		ENDIF
	ENDIF
ENDIF


@COM_ORDER_COM96, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の요정素を考慮
;(순종が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:새드끼
IF ABL:새드끼
	LOCAL += ABL:새드끼 * 5
	LOCALS = %LOCALS% + %ABLNAME:15%LV{ABL:새드끼}({ABL:새드끼 * 5})
ENDIF
;ABL:봉사정신
IF ABL:봉사정신
	LOCAL += ABL:봉사정신 * 4
	LOCALS = %LOCALS% + %ABLNAME:6%LV{ABL:봉사정신}({ABL:봉사정신 * 4})
ENDIF

;부끄럼쟁이
IF TALENT:부끄럼쟁이
	LOCAL -= 5
	LOCALS = %LOCALS% - %TALENTNAME:35%(5)
ENDIF
;헌신的
IF TALENT:헌신
	LOCAL += 6
	LOCALS = %LOCALS% + %TALENTNAME:63%(6)
ENDIF
;쾌감을부정
IF TALENT:쾌감을부정
	LOCAL -= 1
	LOCALS = %LOCALS% - %TALENTNAME:71%(1)
ENDIF

;불결있음
IF COM_ORDER_STAIN("Ａ", "Ｍ")
	LOCAL -= COM_ORDER_STAIN("Ａ", "Ｍ")
	LOCALS = %LOCALS% - 불결있음
	;악취둔감
	SIF TALENT:악취둔감 || TALENT:악취민감
		LOCALS = %LOCALS%, \@TALENT:악취둔감 ? %TALENTNAME:61% # %TALENTNAME:62%\@
	LOCALS = %LOCALS%({COM_ORDER_STAIN("Ａ", "Ｍ")})
ENDIF

;合計を表示(40이상で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 40

RETURN RESULT
