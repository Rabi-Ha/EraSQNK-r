@COM107
;역Ａ바이브
;PLAYER側が바이브で楽しむ커맨드. 通常の바이브커맨드とは違い, 入れっぱなしにはしない. 
PRINT 역Ａ바이브
SIF TALENT:PLAYER:요정
	PRINT (요정용)
PRINTL 

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 107

;実行できない
SIF TCVAR:28 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:0 = 역Ａ바이브

;-------------------------------------------------
;사정체크
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1

@TRAIN_MESSAGE_COM107
;바이브선택
CALL SELECT_VIB_A, PLAYER

SIF REFUSE_CHECK()
	RETURN 0

PRINTFORM %조사처리(CALLNAME:TARGET,"는")%, %CALLNAME:PLAYER%의 애널을 
SELECTCASE SIZE("Ａ바이브", PLAYER)
CASE IS >= 3
	PRINT 극태 바이브
CASE 2
	PRINT 굵은 바이브
CASE 1
	PRINT 바이브
CASE 0
	PRINT 애널바이브
ENDSELECT
IF PREVCOM == SELECTCOM && COND("前Ｔ조교자") == PLAYER
	PRINTW 로 계속 휘젓고 있다…
ELSE
	PRINTW 로 휘저었다…
ENDIF


@FLAG_COM107, ARG

@SOURCE_COM107, ARG
VARSET LOCAL

LOCAL:20 += 100
LOCAL:21 += 100

LOCAL:3 = 500
LOCAL:8 = 1000
LOCAL:11 = CALCF("실행치による욕정추가")
LOCAL:14 = 1500
;바이브の大きさなど
IF SIZE("Ａ바이브", PLAYER) >= 3
	TIMES LOCAL:11 , 3.00
ELSEIF SIZE("Ａ바이브", PLAYER) >= 2
	TIMES LOCAL:11 , 1.50
ENDIF
IF SIZE("Ａ바이브", PLAYER) > ABL:PLAYER:Ａ확장
	TIMES LOCAL:11 , 1.50
ELSEIF SIZE("Ａ바이브", PLAYER) < ABL:PLAYER:Ａ확장
	TIMES LOCAL:11 , 0.50
ENDIF

;ABL:봉사정신をみる
SELECTCASE ABL:봉사정신
CASE 0
	LOCAL:4 = 420
	LOCAL:5 = 150
CASE 1
	LOCAL:4 = 500
	LOCAL:5 = 300
CASE 2
	LOCAL:4 = 580
	LOCAL:5 = 600
CASE 3
	LOCAL:4 = 660
	LOCAL:5 = 900
CASE 4
	LOCAL:4 = 740
	LOCAL:5 = 1500
CASEELSE
	LOCAL:4 = 820
	LOCAL:5 = 2200
ENDSELECT

;ABL:기교をみる
SELECTCASE ABL:기교
CASE 0
	TIMES LOCAL:4 , 0.50
	TIMES LOCAL:5 , 0.50
CASE 1
	TIMES LOCAL:4 , 0.80
	TIMES LOCAL:5 , 0.80
CASE 2
CASE 3
	TIMES LOCAL:4 , 1.20
	TIMES LOCAL:5 , 1.20
CASE 4
	TIMES LOCAL:4 , 1.50
	TIMES LOCAL:5 , 1.50
CASEELSE
	TIMES LOCAL:4 , 2.00
	TIMES LOCAL:5 , 2.00
ENDSELECT

CALL CALC, "조교소스", LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13, LOCAL:14, LOCAL:15, LOCAL:16, LOCAL:17, LOCAL:18, LOCAL:19, LOCAL:20, LOCAL:21, ARG


@COM_ORDER_COM107, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の요정素を考慮
;(순종が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:봉사정신
IF ABL:봉사정신
	LOCAL += ABL:봉사정신 * 4
	LOCALS = %LOCALS% + %ABLNAME:6%LV{ABL:봉사정신}({ABL:봉사정신 * 4})
ENDIF
;ABL:새드끼
IF ABL:새드끼
	LOCAL += ABL:새드끼 * 5
	LOCALS = %LOCALS% + %ABLNAME:15%LV{ABL:새드끼}({ABL:새드끼 * 5})
ENDIF
;헌신的
IF TALENT:헌신
	LOCAL += 6
	LOCALS = %LOCALS% + %TALENTNAME:63%(6)
ENDIF
;쾌감을부정
IF TALENT:쾌감을부정
	LOCAL -= 1
	LOCALS = %LOCALS% - %TALENTNAME:71%(1)
ENDIF
;새드
IF TALENT:새드
	LOCAL += 5
	LOCALS = %LOCALS% + %TALENTNAME:83%(5)
ENDIF


;合計を表示(40이상で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 40

RETURN RESULT
