;=============================================================================
;소환사고用ステータス生成処理
;=============================================================================
@CHAR_SAMON_T, ARG
;현재起こりうる事故(ビットで管理)
;0	처녀
;1	上級몽마
;2	몽마でない
;3	마법を使える
;4	歌が歌える
;5	変わり種
;6	반하기쉬움
;7	レズ嗜好
;8	尽くすタイプ
;9	도착적
;10	高난이도
;11	거유
;12 	빈유
;13 	장신
;14	小柄
;15	동물耳
;16	오토코노코
;17	후타나리

P = 1p10
R = 2
;처녀
IF TALENT:처녀 == 0
	SIF RAND:R == 0
		P = 1p0
	R += 1
ENDIF
;상위몽마
IF TALENT:몽마
	SIF RAND:R == 0
		P = 1p1
	R += 1
ENDIF
;몽마で無い（これだけ확률が低い）
IF TALENT:몽마 + TALENT:상위몽마 && RAND:10 == 0
	SIF RAND:R == 0
		P = 1p2
	R += 1
ENDIF
;마법사
IF TALENT:마법사 == 0
	SIF RAND:R == 0
		P = 1p3
	R += 1
ENDIF
;가창기능
IF TALENT:가창기능 == 0
	SIF RAND:R == 0
		P = 1p4
	R += 1
ENDIF
;変り種
	SIF RAND:R == 0
		P = 1p5
	R += 1
;반하기쉬움
IF TALENT:반하기쉬움 == 0
	SIF RAND:R == 0
		P = 1p6
	R += 1
ENDIF
;レズッ気
IF TALENT:바이 == 0
	SIF RAND:R == 0
		P = 1p7
	R += 1
ENDIF
;尽くすタイプ
IF TALENT:헌신 == 0
	SIF RAND:R == 0
		P = 1p8
	R += 1
ENDIF
;도착적
IF TALENT:도착적 == 0
	SIF RAND:R == 0
		P = 1p9
	R += 1
ENDIF

SIF FLAG:666 && TALENT:몽마
	P = 1p1

;거유
IF TALENT:거유 < 2 && RAND:5 == 0
	SETBIT P, 11
;빈유
ELSEIF TALENT:빈유 == 0 && RAND:5 == 0
	SETBIT P, 12
ENDIF
;장신
IF TALENT:장신 + TALENT:요정 == 0 && RAND:5 == 0
	SETBIT P, 13
;小柄
ELSEIF TALENT:작은몸집 == 0 && RAND:5 == 0
	SETBIT P, 14
ENDIF
;동물耳
SIF COND("인외度") == 0 && RAND:5 == 0
	SETBIT P, 15
;오토코노코と후타나리
IF PENIS(TARGET) == 0 && RAND:5 == 0
	IF CONFIG("소환사고：오토코노코") && AGE(TARGET) < 3 && RAND:2
		SETBIT P, 16
		CLEARBIT P, 0
		CLEARBIT P, 7
		CLEARBIT P, 11
		CLEARBIT P, 12
		CLEARBIT P, 13
		CLEARBIT P, 17
	ENDIF
	IF CONFIG("소환사고：후타나리") && RAND:2
		SETBIT P, 17
		CLEARBIT P, 16
	ENDIF
ENDIF

;소환천재（天才）
IF TALENT:MASTER:소환천재（天才） && FLAG:8
	P = 0
	CALL CHAR_SAMON_T_EX
	SIF P == 0
		RETURN 0
ENDIF

VARSET LOCALS
;ここのツリーではLOCALS:1～LOCALS:5を使う
;上級몽마
IF GETBIT(P, 1)
	LOCALS:1 = 예정보다 상위의 몽마가 소환된 것같다
	;その種に상위몽마がいるかを체크
	IF TALENT:상위몽마
		LOCALS:2 = …아무래도, 대단한 사고는 아니었나보다
	ELSE
		IF IS_HIGH_RANKER(ARG)
			;名前や能力が変わるかも？
			E = RAND:3 + 2
			CALL HIGH_RANKER, ARG, E
			CALL SET_NEWNAME
		ENDIF
		SIF CFLAG:8 == 0
			CFLAG:8 = RAND:4 + 1
	ENDIF
	ABL:ＬＶ = 0
	MAXBASE:체력 += 500
	MAXBASE:기력 += 300
	TALENT:몽마 = 0
	TALENT:상위몽마 = 1
	;TALENT:강기 = 1
	SIF RAND:10 == 0
		TALENT:금단의지식 = 1
	SIF RAND:10 == 0
		TALENT:매력 = 1
	SIF RAND:10 == 0
		TALENT:매혹 = 1
	
	IF TALENT:마법사 == 1
		LOCALS:3 = 이미 상당수의 마법도 쓸 수 있는 것같다! 
		TALENT:미니멈 = 1
		TALENT:센시브 = 1
		TALENT:우르즈군 = 1
		TALENT:이사군 = RAND:2
		TALENT:아르스이사 = RAND:2
		CALL GET_MAGIC, TARGET
	ENDIF
;몽마でない
ELSEIF GETBIT(P, 2)
	LOCALS:1 = 몽마가 되지않은 마물이 소환되었다
	SIF ARG == 1 || ARG == 2 || ARG == 34 || ARG == 35 || ARG == 48 || ARG == 50
		LOCALS:2 = …………………에? 
	MAXBASE:체력 -= 50
	MAXBASE:기력 += 100
	TALENT:상위몽마 = 0
	TALENT:몽마 = 0
	TALENT:정조관결여 = 0
	TALENT:부끄럼없음 = 0
;마법を使える
ELSEIF GETBIT(P, 3)
	LOCALS:1 = …마법을 쓸 수 있는 몽마가 소환되었다
	TALENT:마법사 = 1
	CALL GET_MAGIC, TARGET
	MAXBASE:기력 += 300
;歌が歌える
ELSEIF GETBIT(P, 4)
	LOCALS:1 =  …노래를 부를 수 있는 몽마가 소환되었다
	TALENT:가창기능 = 1
	MAXBASE:기력 += 300
;変わり種
ELSEIF GETBIT(P, 5)
	LOCALS:1 =  조금 독특한 종을 소환하고말았다
	MAXBASE:체력 += 100 * RAND:11
	MAXBASE:기력 += 100 * RAND:11

	REPEAT 22
		R = 0
		SELECTCASE COUNT
		CASE 0
			R = 23
		CASE 1
			R = 10
		CASE 2
			R = 25
		CASE 3
			R = 35
		CASE 4
			R = 40
		CASE 5
			R = 42
		CASE 6
			R = 50
		CASE 7
			R = 61
		CASE 8
			R = 11
		CASE 9
			R = 101
		CASE 10
			R = 103
		CASE 11
			R = 105
		CASE 12
			R = 107
		CASE 13
			R = 111
		CASE 14
			R = 32
		CASE 15
			R = 115
		CASE 16
			R = 70
		CASE 17
			R = 81
			;유치・소악마
			IF RAND:4 == 0
				TALENT:유치 = 1
			ELSEIF RAND:4 == 0
				TALENT:소악마 = 1
			ENDIF
		CASE 18
			R = 15
		CASE 19
			R = 30
		CASE 20
			;マイナス소질のオンパレード
			IF RAND:2 == 0
				;질투
				SIF RAND:4 == 0
					TALENT:질투 = 1
				SIF RAND:4 == 0
					TALENT:일선을넘지않음 = 1
				;저항
				SIF RAND:8 == 0
					TALENT:저항 = 1
				;감정결핍
				SIF RAND:8 == 0 && TALENT:감정결핍 == 0
					TALENT:감정결핍 = 1
			ENDIF
		CASE 21
			;プラス소질のオンパレード
			;고무
			SIF RAND:4 == 0
				TALENT:고무 = 1
			;바늘다루기
			SIF RAND:10 == 0
				TALENT:바늘다루기 = 1
			;도착적
			SIF RAND:10 == 0
				TALENT:도착적 = 1
			SIF RAND:4 == 0
				TALENT:새드 = 1
			;매력・바이
			SIF RAND:4 == 0
				TALENT:매력 = 1
		ENDSELECT

		SIF RAND:4 || R == 0 || CALCF("逆の소질", R) == 0
			CONTINUE

		SELECTCASE RAND:3
		CASE 2
			TALENT:R = 1
			TALENT:CALCF("逆の소질", R) = 0
		CASE 1
			TALENT:R = 0
			TALENT:CALCF("逆の소질", R) = 0
		CASE 0
			TALENT:R = 0
			TALENT:CALCF("逆の소질", R) = 1
		ENDSELECT
	REND
;반하기쉬움
ELSEIF GETBIT(P, 6)
	LOCALS:1 = 초금 반하기 쉬운 몽마를 소환해버렸다. 
	TALENT:반하기쉬움 = 1
;レズ・바이
ELSEIF GETBIT(P, 7)
	LOCALS:1 = 백합에 눈뜬 몽마를 소환해버렸다
	ABL:레즈끼 = 1 + RAND:3
	EXP:레즈경험 = 10 * RAND:5 + 1
	IF RAND:2 == 0
		TALENT:남성혐오 = 1
		TALENT:바이 = 0
	ELSE
		TALENT:바이 = 1
		TALENT:남성혐오 = 0
	ENDIF
;尽くすタイプ
ELSEIF GETBIT(P, 8)
	LOCALS:1 = 헌신하는 일에 기쁨을 느끼는 몽마가 소환되었다
	MAXBASE:체력 -= 100
	MAXBASE:기력 += 200 * RAND:3
	TALENT:솔직 = 1
	TALENT:현명함 = 1
	TALENT:헌신 = 1
	TALENT:치료 = 1
	TALENT:고무 = 1

	TALENT:반항적 = 0
	TALENT:건방짐 = 0
	TALENT:미련함 = 0
;도착적
ELSEIF GETBIT(P, 9)
	LOCALS:1 = 도착적인 성격의 몽마가 소환되었다
	TALENT:도착적 = 1
	IF RAND:3 == 0
		LOCALS:2 = …게다가, 마조기질을 가진 것같다! 
		ABL:마조끼 += RAND:3 + 1
		SIF ABL:마조끼 >= 5
			ABL:마조끼 = 5
		ABL:새드끼 -= RAND:3
		IF ABL:새드끼 <= 0
			ABL:새드끼 = 0
			TALENT:새드 = 0
		ENDIF
		SIF RAND:10 < ABL:마조끼
			TALENT:마조 = 1
	ELSEIF RAND:2 == 0
		LOCALS:2 = …게다가, 새드기질을 가진 것같다! 
		ABL:새드끼 += RAND:3 + 1
		SIF ABL:새드끼 >= 5
			ABL:새드끼 = 5
		ABL:마조끼 -= RAND:3
		IF ABL:마조끼 <= 0
			ABL:마조끼 = 0
			TALENT:마조 = 0
		ENDIF
		SIF RAND:10 < ABL:새드끼
			TALENT:새드 = 1
	ENDIF
;난이도高
ELSEIF GETBIT(P, 10)
	LOCALS:1 = 얼핏보면, 평범해보이지만, 조금 귀찮은 타입인 것같다
	MAXBASE:체력 += 100 * RAND:6
	MAXBASE:기력 += 300 * RAND:4
	;남성혐오・질투
	IF RAND:4 == 0
		TALENT:남성혐오 = 1
	ELSEIF RAND:5 == 0
		TALENT:질투 = 1
	ENDIF
	;쾌감을부정・일선을넘지않음
	IF RAND:8 == 0
		TALENT:쾌감을부정 = 1
		TALENT:CALCF("逆の소질", 71) = 0
	ENDIF
	IF RAND:8 == 0
		TALENT:일선을넘지않음 = 1
		TALENT:CALCF("逆の소질", 27) = 0
	ENDIF
	;억압・보수적
	IF RAND:6 == 0
		TALENT:억압 = 1
		TALENT:CALCF("逆の소질", 32) = 0
	ENDIF
	IF RAND:6 == 0
		TALENT:보수적 = 1
		TALENT:CALCF("逆の소질", 24) = 0
	ENDIF
	;저항・반항적
	SIF RAND:4 == 0 && TALENT:솔직 == 0
		TALENT:반항적 = 1
	SIF RAND:4 == 0
		TALENT:저항 = 1
	;감정결핍
	SIF RAND:8 == 0 && TALENT:감정결핍 == 0
		TALENT:감정결핍 = 1
ENDIF

;身体のサイズ変化
;LOCALS:6～LOCALS:10を用いる
SIF P&(1p11 + 1p12 + 1p13 + 1p14)
	LOCALS:6 = 같은 종족중에서도, 
;거유
IF GETBIT(P, 11)
	R = 1
	IF TALENT:빈유
		R += 2
	ELSEIF TALENT:거유 == 0
		R += 1
	ENDIF
	R = 1 + RAND:R

	IF R == 1
		LOCALS:6 = %LOCALS:6% 가슴의 부푼정도가 더 풍만
		IF P&(1p13 + 1p14)
			LOCALS:6 = %LOCALS:6%하고, 
		ELSE
			LOCALS:6 = %LOCALS:6%한 것 같다
		ENDIF
	ELSE
		LOCALS:6 = %LOCALS:6% 가슴 크기가 빼어
		IF P&(1p13 + 1p14)
			LOCALS:6 = %LOCALS:6%나게 크고, 
		ELSE
			LOCALS:6 = %LOCALS:6%난 것 같다
		ENDIF
	ENDIF

	IF (TALENT:빈유 && R == 3) || TALENT:거유 || (TALENT:빈유 + TALENT:거유 == 0 && R == 2)
		CALL CALC, "폭유化", TARGET
	ELSEIF (TALENT:빈유 && R == 2) || (TALENT:빈유 + TALENT:거유 == 0 && R == 1)
		CALL CALC, "거유化", TARGET
	ELSE
		CALL CALC, "평유化", TARGET
	ENDIF
;빈유
ELSEIF GETBIT(P, 12)
	R = 1
	SIF TALENT:거유
		R += TALENT:거유
	R = 1 + RAND:R

	IF R == 1
		LOCALS:6 = %LOCALS:6% 가슴의 부푼 정도가 어른스
		IF P&(1p13 + 1p14)
			LOCALS:6 = %LOCALS:6%럽고, 
		ELSE
			LOCALS:6 = %LOCALS:6%러운 것 같다
		ENDIF
	ELSE
		LOCALS:6 = %LOCALS:6% 가슴의 평평함이 특이
		IF P&(1p13 + 1p14)
			LOCALS:6 = %LOCALS:6%하고, 
		ELSE
			LOCALS:6 = %LOCALS:6%한 것같다
		ENDIF
	ENDIF

	IF TALENT:거유 >= 2 && R == 1
		CALL CALC, "거유化", TARGET
	ELSEIF TALENT:거유 && R == TALENT:거유
		CALL CALC, "평유化", TARGET
	ELSE
		CALL CALC, "빈유化", TARGET
	ENDIF
	;파이즈리경험軽減
	EXP:파이즈리경험 /= 3
ENDIF
;장신
IF GETBIT(P, 13)
	R = 1
	SIF TALENT:작은몸집
		R += 1
	R = 1 + RAND:R

	IF R == 1
		LOCALS:6 = %LOCALS:6% 키가 큰 것 같다
	ELSE
		LOCALS:6 = %LOCALS:6% 이상할 정도로 키가 큰 것 같다
	ENDIF
	IF TALENT:작은몸집 && R == 1
		CALL CALC, "보통体型化", TARGET
	ELSE
		CALL CALC, "장신化", TARGET
	ENDIF
;小柄
ELSEIF GETBIT(P, 14)
	R = 1
	SIF TALENT:장신
		R += 1
	R = 1 + RAND:R

	IF R == 1
		LOCALS:6 = %LOCALS:6% 조금 키가 작아보인다
	ELSE
		LOCALS:6 = %LOCALS:6% 예외적으로 신장이 작아보인다…
	ENDIF
	IF TALENT:장신 && R == 1
		CALL CALC, "보통体型化", TARGET
	ELSE
		CALL CALC, "小柄化", TARGET
	ENDIF
ENDIF

;오토코노코と후타나리
;LOCALS:7を使う
IF GETBIT(P, 16)
	TALENT:오토코노코 = 1
	SIF TALENT:장신
		CALL CALC, "보통体型化", TARGET
	TALENT:여자 = 0
	TALENT:후타나리 = 0
	TALENT:남자 = 1
	EXP:Ｖ경험 = 0
	EXP:Ｖ성교경험 = 0
	EXP:질내정액경험 = 0
	EXP:파이즈리경험 = 0
	EXP:레즈경험 = 0
	EXP:Ｖ확장경험 = 0
	EXP:Ｖ손상경험 = 0
	EXP:Ｖ피스트경험 = 0
	LOCALS:7 = …잠깐, 이건……설마…여자라고 생각했는데 남자? ! 
ELSEIF GETBIT(P, 17)
	TALENT:여자 = 0
	TALENT:후타나리 = 1
	TALENT:남자 = 0
	LOCALS:7 = !？…잠깐, 이건……있어서는 안되는 물건을 갖고 있는 것같다…!! 
ENDIF

;동물耳
;LOCALS:11を使う
IF GETBIT(P, 15)
	TALENT:동물귀 = 1 + RAND:5
	TALENT:꼬리 = TALENT:동물귀
	LOCALS:11 = \@ P == 1p15 ? 게다가 # 더욱이 \@, %NAMES("動物の種類", TALENT:동물귀)%에게 귀와 꼬리가 나있다…! ? 
ENDIF

LOCAL = 0
;簡易처녀化
;LOCALS:12,LOCALS:13を使う
IF P == 1
	LOCALS:12 = 아직 남성경험이 없는 몽마가 소환된 것같다
	MAXBASE:체력 -= 200
	MAXBASE:기력 -= 100
	SIF RAND:5 == 0
		TALENT:겁쟁이 = 1
	SIF RAND:5 == 0
		TALENT:정조관념 = 1
	SIF RAND:5 == 0
		TALENT:부끄럼쟁이 = 1
	SIF RAND:5 == 0
		TALENT:꿋꿋함 = 0
	SIF RAND:5 == 0 || TALENT:정조관결여
		TALENT:정조관결여 = 0
	SIF RAND:5 == 0 || TALENT:부끄럼쟁이
		TALENT:부끄럼없음 = 0
	IF RAND:3 == 0
		LOCALS:13 = 근데, 아무래도 여성경험은 있는 것 같다
		ABL:레즈끼 = 1 + RAND:4
		EXP:레즈경험 = 10 * RAND:5+1
		IF RAND:5 == 0
			TALENT:남성혐오 = 1
		ELSEIF RAND:4 == 0
			TALENT:바이 = 1
		ENDIF
	ENDIF
	LOCAL = 1
ELSEIF GETBIT(P, 0) || (RAND:4 == 0 && TALENT:처녀 == 0 && TALENT:MASTER:소환천재（天才） == 0)
	LOCALS:12 = ……게다가, 아직 처녀인 모양이다!! 
	LOCAL = 1
ENDIF
IF LOCAL
	TALENT:처녀 = 1
	TALENT:출산경험 = 0
	ABL:Ｖ감각 = 0
	EXP:Ｖ경험 = 0
	EXP:Ｖ성교경험 = 0
	EXP:허리사용경험 = 0
	EXP:Ｖ확장경험 = 0
	BASE:태아의부친 = 0
ENDIF

;身体のサイズ変化
IF P&(1p11 + 1p12 + 1p13 + 1p14)
	BASE:신장 = 0
	BASE:바스트 = 0
	BASE:컵 = 0
	BASE:웨스트 = 0
	BASE:힙 = 0
	BASE:신장의추가요소 = 0
	CALL SET_3SIZE, TARGET
ENDIF

IF TALENT:남자 && EXP:절정경험
	EXP:사정경험 += EXP:절정경험
	EXP:절정경험 = 0
ENDIF

;체력, 기력が上昇していた場合, 全快させる
SIF BASE:체력 != MAXBASE:체력
	BASE:체력 = MAXBASE:체력
SIF BASE:기력 != MAXBASE:기력
	BASE:기력 = MAXBASE:기력

;文章を表示しないならばここまで
SIF FLAG:8 == 0
	RETURN 1

FOR LOCAL, 1, 15
	SIF LOCALS:LOCAL != ""
		PRINTFORMW %LOCALS:LOCAL%
NEXT

RETURN 1


@CHAR_SAMON_T_EX
#DIM MEMO_LINECOUNT
MEMO_LINECOUNT = LINECOUNT
REDRAW 0
PRINTL 어느 소환사고를 일으킵니까? 
PRINTL 또한, 처녀화, 가슴과 신장 사이즈의 사고는, 다른 것과 동시에 발생가능합니다
PRINTL 
SIF TALENT:처녀 == 0
	PRINTFORML  [ 0] \@ GETBIT(P, 0) ? ○ # － \@ 처녀
SIF TALENT:몽마
	PRINTFORML  [ 1] \@ GETBIT(P, 1) ? ○ # － \@ 상위몽마
SIF TALENT:몽마 + TALENT:상위몽마
	PRINTFORML  [ 2] \@ GETBIT(P, 2) ? ○ # － \@ 몽마가 아니게된다
SIF TALENT:마법사 == 0
	PRINTFORML  [ 3] \@ GETBIT(P, 3) ? ○ # － \@ 소질：마법사 소지 
SIF TALENT:가창기능 == 0
	PRINTFORML  [ 4] \@ GETBIT(P, 4) ? ○ # － \@ 소질：가창기능 소지 
	PRINTFORML  [ 5] \@ GETBIT(P, 5) ? ○ # － \@ 변종（소질 등이 바뀜）
SIF TALENT:반하기쉬움 == 0
	PRINTFORML  [ 6] \@ GETBIT(P, 6) ? ○ # － \@ 반하기쉬움
;レズッ気
SIF TALENT:바이 == 0
	PRINTFORML  [ 7] \@ GETBIT(P, 7) ? ○ # － \@ 레즈지향
SIF TALENT:헌신 == 0
	PRINTFORML  [ 8] \@ GETBIT(P, 8) ? ○ # － \@ 헌신적인 타입
SIF TALENT:도착적 == 0
	PRINTFORML  [ 9] \@ GETBIT(P, 9) ? ○ # － \@ 도착적
	PRINTFORML  [10] \@ GETBIT(P, 10) ? ○ # － \@ 조교난이도가 높음
SIF TALENT:거유 < 2
	PRINTFORML  [11] \@ GETBIT(P, 11) ? ○ # － \@ 거유화
SIF TALENT:빈유 == 0
	PRINTFORML  [12] \@ GETBIT(P, 12) ? ○ # － \@ 빈유화
SIF TALENT:장신 + TALENT:요정 == 0
	PRINTFORML  [13] \@ GETBIT(P, 13) ? ○ # － \@ 장신화
SIF TALENT:작은몸집 == 0
	PRINTFORML  [14] \@ GETBIT(P, 14) ? ○ # － \@ 작은 체형화
SIF COND("인외度") == 0
	PRINTFORML  [15] \@ GETBIT(P, 15) ? ○ # － \@ 동물귀있음
SIF PENIS(TARGET) == 0 && AGE(TARGET) < 3
	PRINTFORML  [16] \@ GETBIT(P, 16) ? ○ # － \@ 오토코노코(외형이 어린 캐릭터 한정)
SIF PENIS(TARGET) == 0
	PRINTFORML  [17] \@ GETBIT(P, 17) ? ○ # － \@ 후타나리화

PRINTFORML [100] \@ P ? 이걸로 ＯＫ # 사고는 일어나지 않아 \@

$INPUT_LOOP
INPUT

LOCAL = RESULT

IF LOCAL == 100
	REDRAW 1
	RETURN 0
ELSEIF LOCAL == 0 && TALENT:처녀 == 0
	CLEARBIT P, 16
ELSEIF LOCAL == 1 && TALENT:몽마
ELSEIF LOCAL == 2 && TALENT:몽마 + TALENT:상위몽마
ELSEIF LOCAL == 3 && TALENT:마법사 == 0
ELSEIF LOCAL == 4 && TALENT:가창기능 == 0
ELSEIF LOCAL == 5
ELSEIF LOCAL == 6 && TALENT:반하기쉬움 == 0
ELSEIF LOCAL == 7 && TALENT:바이 == 0
	CLEARBIT P, 16
ELSEIF LOCAL == 8 && TALENT:헌신 == 0
ELSEIF LOCAL == 9 && TALENT:도착적 == 0
ELSEIF LOCAL == 10
ELSEIF LOCAL == 11 && TALENT:거유 < 2
	CLEARBIT P, 12
	CLEARBIT P, 16
ELSEIF LOCAL == 12 && TALENT:빈유 == 0
	CLEARBIT P, 11
	CLEARBIT P, 16
ELSEIF LOCAL == 13 && TALENT:장신 + TALENT:요정 == 0
	CLEARBIT P, 14
	CLEARBIT P, 16
ELSEIF LOCAL == 14 && TALENT:작은몸집 == 0
	CLEARBIT P, 13
ELSEIF LOCAL == 15 && COND("인외度") == 0
ELSEIF LOCAL == 16 && PENIS(TARGET) == 0 && AGE(TARGET) < 3
	CLEARBIT P, 0
	CLEARBIT P, 7
	CLEARBIT P, 11
	CLEARBIT P, 12
	CLEARBIT P, 13
	CLEARBIT P, 17
ELSEIF LOCAL == 17 && PENIS(TARGET) == 0
	CLEARBIT P, 16
ELSE
	GOTO INPUT_LOOP
ENDIF

INVERTBIT P, LOCAL

IF LOCAL >= 1 && LOCAL <= 10
	FOR LOCAL:1, 1, 11
		SIF LOCAL:1 == LOCAL
			CONTINUE
		CLEARBIT P, LOCAL:1
	NEXT
ENDIF
CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

